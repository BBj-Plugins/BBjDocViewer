use ::BBjDocViewer/BBjDocViewer.bbj::BBjDocViewer
? 'HIDE'

wnd! = BBjAPI().openSysGui("X0").addWindow(10,10,900,600,"BBj Document Viewer Demo")



handTool!  = wnd!.addButton(211,5,5,150,25,"Open Document")
handTool!.setCallback(BBjAPI.ON_BUTTON_PUSH,"onOpenDocument")

handToolBlob!  = wnd!.addButton(212,160,5,150,25,"Open Document (as BLOB)")
handToolBlob!.setCallback(BBjAPI.ON_BUTTON_PUSH,"onOpenDocumentBlob")


viewer! = new BBjDocViewer(wnd!,100,0,40,895,555)
viewer!.open(BBjAPI().getFileSystem().resolvePath("BBjDocViewer/Demo/compressed.tracemonkey-pldi-09.pdf"))

wnd!        .setCallback(BBjAPI.ON_CLOSE,"byebye")
wnd!        .setCallback(BBjAPI.ON_RESIZE,"resize")

process_events

byebye:
bye


onOpenDocument:
REM Build filters for brc/arc and all files
  FILTER$="PDF Files"+$0a$+"*.pdf"
  FILTER$=FILTER$+$0a$+"All Files (*.*)"+$0a$+"*.*"

  REM Starting directory is the default Directory
  FILE_DIR$=""

  REM Use FILEOPEN() to get name of file to open
  FILE_NAME$=FILEOPEN("Open Resource File",FILE_DIR$,"","",FILTER$)

  if FILE_NAME$ <> "::CANCEL::" then
    viewer!.open(FILE_NAME$)
  fi
return


onOpenDocumentBlob:
REM Build filters for brc/arc and all files
  FILTER$="PDF Files"+$0a$+"*.pdf"
  FILTER$=FILTER$+$0a$+"All Files (*.*)"+$0a$+"*.*"

  REM Starting directory is the default Directory
  FILE_DIR$=""

  REM Use FILEOPEN() to get name of file to open
  FILE_NAME$=FILEOPEN("Open Resource File",FILE_DIR$,"","",FILTER$)

  if FILE_NAME$ <> "::CANCEL::" then
    ch=unt
    open (ch)FILE_NAME$ 
    read record (ch,siz=dec(fin(ch)(1,4)))blob$
    close (ch)
    p! = java.nio.file.Paths.get(file_name$)
    file_name$ = p!.getFileName().toString()
    
    viewer!.openBlob(blob$,file_name$)
  fi
return


resize:
    BBjAPI().removeTimer("BBjDocViewer_delayed_resize",err=*next)
    BBjAPI().createTimer("BBjDocViewer_delayed_resize",.2,"delayed_resize")
    ev! = BBjAPI().getLastEvent()
    w=ev!.getWidth()
    h=ev!.getHeight()
return
    
delayed_resize:
    BBjAPI().removeTimer("BBjDocViewer_delayed_resize",err=*next)
    viewer!.setSize(w-5,h-45)
return

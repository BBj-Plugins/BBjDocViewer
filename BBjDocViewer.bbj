use com.basiscomponents.db.ResultSet
use com.basiscomponents.db.DataRow
use com.basiscomponents.db.tree.DataTree
use java.sql.Types
use com.google.gson.Gson
use com.google.gson.JsonParser
use com.google.gson.JsonObject
use com.google.gson.JsonArray
use java.lang.Integer
use java.util.ArrayList
use java.util.HashMap
use java.lang.Object
use com.basis.bbj.proxies.event.BBjCustomEvent
use com.google.gson.Gson
use com.google.gson.JsonParser
use com.google.gson.JsonObject
use com.google.gson.JsonArray
use com.basiscomponents.comm.WsServer
use com.basiscomponents.comm.CrossEventDispatcher
use ::BBjWidget/BBjWidget.bbj::BBjWidget
rem /**
rem  * A Grid Widget Plugin for BBj
rem  */
class public BBjDocViewer extends BBjWidget

    field protected BBjHtmlView HTMLView!
    field protected Boolean IsViewerInitialized! = BBjAPI.FALSE
    field protected Boolean IsOpening! = BBjAPI.FALSE
    field protected BBjVector backlog! = new BBjVector()
    field protected BBjString Tag$
    field protected WsServer WS!
    field protected ResultSet RS!
    field protected Boolean Toolbar! = BBjAPI.TRUE
    field protected Boolean HandTool! = BBjAPI.FALSE

    rem /**
    rem  * disabled default constructor
    rem  */
    method private BBjDocViewer()
    methodend
    rem /**
    rem  * The constructor that creates the widget on wnd!
    rem * @param BBjWindow wnd!: parent window
    rem * @param BBjInt id: the control ID
    rem * @param BBjInt x: x-location
    rem * @param BBjInt y: y-location
    rem * @param BBjInt w: width
    rem * @param BBjInt h: height
    rem */
    method public BBjDocViewer(BBjWindow wnd!, BBjNumber id!, BBjNumber x!, BBjNumber y!, BBjNumber w!, BBjNumber h!)
        #super!.create(wnd!,id!,x!,y!,w!,h!)

        WsServer.DEBUG = BBjAPI.TRUE
        
        ed! = CrossEventDispatcher.getInstance(BBjAPI())
        tag$ = str(ed!.register(BBjAPI()))
        #Tag$ = tag$
        ws! = WsServer.getInstance(BBjAPI(),8887)
        ws!.setEventDispatcher(ed!)
        #WS!= cast(WsServer,ws!)
        BBjAPI().setCustomEventCallback(#Tag$,#this! , "onMessage")
    methodend
    rem /**
    rem  * @Override
    rem  * This method is called whenever the widget needs to be rendered
    rem  * @param Boolean init!: if TRUE trem /**
    rem  *
    rem  * control is rendered for the first time so this method has to perform initial rendering
    rem  */
    method public void redraw(Boolean init!)
        declare BBjHtmlView htmlview!

        if (init!) then

            htmlview! = #getCanvas().addHtmlView(101,0,0,#getCanvas().getWidth(),#getCanvas().getHeight(),"")
            #HTMLView!=htmlview!
            
            if (htmlview!.getClientType() <> "Chromium" AND INFO(3,6)<>"5")
                htmlview!.setText("<html><center>Error: Need the Chromium Engine in BBj 18.10 or later. Please check your BBj SAM coverage!</center></html>")
            else
                htmlview!.setOpaque(0)
                htmlview!.setCallback(BBjAPI.ON_PAGE_LOADED,#this!,"onInit")
            fi
            
            if INFO(3,6)<>"5" then
                #HTMLView!.setLocation(-9,-9)
            FI
            
REM             if (info(3,6)<>"5") then
REM                 url$ = htmlview!.getAttribute("remoteDebuggingURL")
REM                 if url$>"" then
REM                     BBjAPI().getThinClient().browse(url$)
REM                 fi
REM             fi
        FI

        if INFO(3,6)="5" then
            #HTMLView!.setSize(#getCanvas().getWidth(),#getCanvas().getHeight())
        else
            #HTMLView!.setSize(#getCanvas().getWidth()+16,#getCanvas().getHeight()+16)
        FI
    methodend

    method public void onInit(BBjEvent ev!)
        #HTMLView!.setCallback(BBjAPI.ON_PAGE_LOADED,#this!,"onLoad")
        hash! = java.util.UUID.randomUUID().toString()
        bui! = "&"
        if (info(3,6)="5") then bui! = bui! + "bui=true"
        #HTMLView!.setUrl("http://"+info(3,4)+":8888/docviewer/web/basis-viewer.html?file=&_nc=" + str(hash!) + "&_socket=" + "ws://"+info(3,4)+":8887/"+#Tag$ + bui!)
    methodend

    method public void onLoad(BBjEvent ev!)
        #HTMLView!.clearCallback(#HTMLView!.ON_PAGE_LOADED)
    methodend

    method public void onMessage(BBjCustomEvent ev!)
        msg! = ev!.getObject()
        parser! = new JsonParser()
        type! = parser!.parse(msg!).getAsJsonObject().get("type").getAsString()
        detail! = parser!.parse(msg!).getAsJsonObject().get("detail").getAsJsonObject()
        switch type!
            case "initialized"
                #IsViewerInitialized! = BBjAPI.TRUE
                if #isReady() then #flushBacklog() fi
            case "opening"
            case "opened"
            case "openFailed"
        swend
    methodend

    method public void open(BBjString file$)

        dest_file$=file$
        while pos("/"=dest_file$)>0
            dest_file$=dest_file$(pos("/"=dest_file$)+1)
        wend
        while pos("\"=dest_file$)>0
            dest_file$=dest_file$(pos("\"=dest_file$)+1)
        wend
        
        dest_file$=java.net.URLEncoder.encode(dest_file$).replaceAll("\+", "%20").replaceAll("%","_")
        
        rem if rev>"REV 18.10" then
        rem     url$ = ::BBUtils.bbj::BBUtils.copyFileToWebServer(file$,"",dest_file$,0,1,err=*next)
        rem fi
        
        if (url$="") then
            rem falling back for versions <=18.10 where the BBUtils did not have the extra option
            rem for temporary obfuscated URLs; this might be a dev build that doesn't have this yet
            url$ = ::BBUtils.bbj::BBUtils.copyFileToWebServer(file$,"",dest_file$,0)
        fi
        
        detail! = new JsonObject()
        detail!.addProperty("path",url$)
        #emit("dv_open" , detail!)
        
    methodend

    method public void zoomIn()
        #emit("dv_zoomIn")
    methodend

    method public void zoomOut()
        #emit("dv_zoomOut")
    methodend

    method public void setPage(BBjNumber page!)
        detail! = new JsonObject()
        detail!.addProperty("page",str(page!))
        #emit("dv_setPage",detail!)
    methodend

    method public void next()
        #emit("dv_nextPage")
    methodend

    method public void previous()
        #emit("dv_prevPage")
    methodend

    method public void showToolbar()
        #Toolbar! = BBjAPI.TRUE
        #emit("dv_showToolbar")
    methodend

    method public void hideToolbar()
        #Toolbar! = BBjAPI.FALSE
        #emit("dv_hideToolbar")
    methodend

    method public void toggleToolbar()
        if  #Toolbar!= BBjAPI.TRUE then
            #hideToolbar()
        else
            #showToolbar()
        fi
    methodend

    method public void toggleSidebar()
        #emit("dv_toggleSidebar")
    methodend

    method public void activateHandTool()
        #HandTool! = BBjAPI.TRUE
        #emit("dv_activateHandtool")
    methodend

    method public void deactivateHandTool()
        #HandTool! = BBjAPI.FALSE
        #emit("dv_deativateHandtool")
    methodend

    method public void toggleHandTool()
        if  #HandTool!= BBjAPI.TRUE then
            #deactivateHandTool()
        else
            #activateHandTool()
        fi
    methodend

    method protected Boolean isReady()
        methodret  #IsViewerInitialized!
    methodend

    method protected void flushBacklog()
        it! = #backlog!.iterator()
        while it!.hasNext()
            s$=it!.next()
            #WS!.send(#Tag$,s$)
        wend

        #backlog! = new BBjVector()
    methodend

    method protected void emit(BBjString type$ , JsonObject detail!)
        event! = new JsonObject()
        event!.addProperty("type",type$)
        event!.add("detail",detail!)

        if #isReady() then
            #WS!.send(#Tag$, event!.toString())
        else
            #backlog!.addItem(event!.toString())
        FI
    methodend

    method protected void emit(BBjString type$)
        #emit(type$,new JsonObject())
    methodend

classend

use com.basiscomponents.db.ResultSet
use com.basiscomponents.db.DataRow
use com.basiscomponents.db.tree.DataTree
use java.sql.Types
use com.google.gson.Gson
use com.google.gson.JsonParser
use com.google.gson.JsonObject
use com.google.gson.JsonArray
use java.lang.Integer
use java.util.ArrayList
use java.util.HashMap
use java.lang.Object
use com.basis.bbj.proxies.event.BBjCustomEvent
use com.google.gson.Gson
use com.google.gson.JsonParser
use com.google.gson.JsonObject
use com.google.gson.JsonArray
use com.basiscomponents.comm.WsServer
use com.basiscomponents.comm.CrossEventDispatcher
use ::BBjWidget/BBjWidget.bbj::BBjWidget
rem /**
rem  * A Grid Widget Plugin for BBj
rem  */
class public BBjDocViewer extends BBjWidget

    field protected BBjHtmlView HTMLView!
    field protected Boolean IsBBjReady! = BBjAPI.FALSE
    field protected Boolean IsViewerInitialized! = BBjAPI.FALSE
    field protected Boolean IsOpening! = BBjAPI.FALSE
    field protected BBjVector backlog! = new BBjVector()
    field protected BBjString Tag$
    field protected WsServer WS!
    field protected ResultSet RS!
    field protected Boolean Toolbar! = BBjAPI.TRUE
    field protected Boolean HandTool! = BBjAPI.FALSE

    rem /**
    rem  * disabled default constructor
    rem  */
    method private BBjDocViewer()
    methodend
    rem /**
    rem  * The constructor that creates the widget on wnd!
    rem * @param BBjWindow wnd!: parent window
    rem * @param BBjInt id: the control ID
    rem * @param BBjInt x: x-location
    rem * @param BBjInt y: y-location
    rem * @param BBjInt w: width
    rem * @param BBjInt h: height
    rem */
    method public BBjDocViewer(BBjWindow wnd!, BBjNumber id!, BBjNumber x!, BBjNumber y!, BBjNumber w!, BBjNumber h!)
        #super!.create(wnd!,id!,x!,y!,w!,h!)
        ed! = CrossEventDispatcher.getInstance(BBjAPI())
        tag$ = str(ed!.register(BBjAPI()))
        #Tag$ = tag$
        ws! = WsServer.getInstance(BBjAPI(),8887)
        ws!.setEventDispatcher(ed!)
        #WS!= cast(WsServer,ws!)
    methodend
    rem /**
    rem  * @Override
    rem  * This method is called whenever the widget needs to be rendered
    rem  * @param Boolean init!: if TRUE trem /**
    rem  *
    rem  * control is rendered for the first time so this method has to perform initial rendering
    rem  */
    method public void redraw(Boolean init!)
        declare BBjHtmlView htmlview!

        if (init!) then
            dummy$=stbl("!OPTIONS","JXBROWSER_HTMLVIEW=TRUE",ERR=*NEXT)
            dummy$=stbl("!OPTIONS","JAVAFX_HTMLVIEW=TRUE",ERR=*NEXT)
            htmlview! = #getCanvas().addHtmlView(101,0,0,#getCanvas().getWidth(),#getCanvas().getHeight(),"")
            #HTMLView!=htmlview!
            htmlview!.setOpaque(0)
            htmlview!.setCallback(BBjAPI.ON_PAGE_LOADED,#this!,"onInit")

            if INFO(3,6)<>"5" then
                #HTMLView!.setLocation(-9,-9)
            FI
        FI

        if INFO(3,6)="5" then
            #HTMLView!.setSize(#getCanvas().getWidth(),#getCanvas().getHeight())
        else
            #HTMLView!.setSize(#getCanvas().getWidth()+16,#getCanvas().getHeight()+16)
        FI
    methodend

    method public void onInit(BBjEvent ev!)
        #HTMLView!.setCallback(BBjAPI.ON_PAGE_LOADED,#this!,"onLoad")
        #HTMLView!.setUrl("http://localhost:8888/docviewer/web/viewer.html?_nc=#disableworker=true" + str(rnd(0)))
    methodend

    method public void onLoad(BBjEvent ev!)
        #HTMLView!.clearCallback(#HTMLView!.ON_PAGE_LOADED)
        BBjAPI().setCustomEventCallback(#Tag$,#this! , "onMessage")
        bui = INFO(3,6)="5"
        ch=unt
        open (ch) "BBjDocViewer/src/pdfjs/init.js"
        read record (ch,siz=5512000)script$
        close (ch)
        addr$="window.SOCKET_URL = 'ws://"+info(3,4)+":8887/"+#Tag$ + "';"
        script$ = addr$ +  script$
        #injectScript(script$)
        rem ch=unt
        rem open (ch) "BBjDocViewer/web/pdfjs/web/debugger.js"
        rem read record (ch,siz=5512000)script$
        rem close (ch)
        rem #injectScript(script$)
        ch=unt
        open (ch) "BBjDocViewer/web/build/pdf.js"
        read record (ch,siz=5512000)script$
        close (ch)
        #injectScript(script$)
        ch=unt
        open (ch) "BBjDocViewer/web/web/viewer.js"
        read record (ch,siz=5512000)script$
        close (ch)
        p_script! = script$
        rem Not working yet
        if bui then
            p_script! = p_script!.replaceAll("document\.","\getDocument()\.")
        fi

        #injectScript(p_script!)
        ch=unt
        open (ch) "BBjDocViewer/src/pdfjs/api.js"
        read record (ch,siz=5512000)script$
        close (ch)
        #injectScript(script$)
        #IsBBjReady! = BBjAPI.TRUE
    methodend

    method public void onMessage(BBjCustomEvent ev!)
        msg! = ev!.getObject()
        parser! = new JsonParser()
        type! = parser!.parse(msg!).getAsJsonObject().get("type").getAsString()
        detail! = parser!.parse(msg!).getAsJsonObject().get("detail").getAsJsonObject()
        switch type!
            case "initialized"
                #IsViewerInitialized! = BBjAPI.TRUE
                if #isReady() then #flushBacklog() fi
            case "open"
                #IsViewerInitialized! = BBjAPI.TRUE
                if #isReady() then #flushBacklog() fi
        swend
    methodend

    method public void open(BBjString file$)
        detail! = new JsonObject()
        detail!.addProperty("path","http://localhost:8888/docviewer/web/" + file$)
        #emit("dv_open" , detail!)
    methodend

    method public void zoomIn()
        #emit("dv_zoomIn")
    methodend

    method public void zoomOut()
        #emit("dv_zoomOut")
    methodend

    method public void setPage(BBjNumber page!)
        detail! = new JsonObject()
        detail!.addProperty("page",str(page!))
        #emit("dv_setPage",detail!)
    methodend

    method public void next()
        #emit("dv_nextPage")
    methodend

    method public void previous()
        #emit("dv_prevPage")
    methodend

    method public void showToolbar()
        #Toolbar! = BBjAPI.TRUE
        #emit("dv_showToolbar")
    methodend

    method public void hideToolbar()
        #Toolbar! = BBjAPI.FALSE
        #emit("dv_hideToolbar")
    methodend

    method public void toggleToolbar()
        if  #Toolbar!= BBjAPI.TRUE then
            #hideToolbar()
        else
            #showToolbar()
        fi
    methodend

    method public void toggleSidebar()
        #emit("dv_toggleSidebar")
    methodend

    rem method public void activateHandTool()
    rem     #HandTool! = BBjAPI.TRUE
    rem     #emit("dv_activateHandtool")
    rem methodend

    rem method public void deactivateHandTool()
    rem     #HandTool! = BBjAPI.FALSE
    rem     #emit("dv_deativateHandtool")
    rem methodend

    rem method public void toggleHandTool()
    rem     if  #HandTool!= BBjAPI.TRUE then
    rem         #deactivateHandTool()
    rem     else
    rem         #activateHandTool()
    rem     fi
    rem methodend

    method protected void injectScript(BBjString script$)
        bui = INFO(3,6)="5"
        if bui then #HTMLView!.injectScript(script$) else #HTMLView!.injectScript(script$ , 1) FI
    methodend

    method protected Boolean isReady()
        methodret (#IsBBjReady! and #IsViewerInitialized!) and #IsOpening! = BBjAPI.FALSE
    methodend

    method protected void flushBacklog()
        it! = #backlog!.iterator()
        while it!.hasNext()
            s$=it!.next()
            #WS!.send(#Tag$,s$)
        wend

        #backlog! = new BBjVector()
    methodend

    method protected void emit(BBjString type$ , JsonObject detail!)
        event! = new JsonObject()
        event!.addProperty("type",type$)
        event!.add("detail",detail!)

        if #isReady() then
            #WS!.send(#Tag$, event!.toString())
        else
            #backlog!.addItem(event!.toString())
        FI
    methodend

    method protected void emit(BBjString type$)
        #emit(type$,new JsonObject())
    methodend

classend
